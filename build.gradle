// Buildscript MUST be the first block in a build.gradle
// file
buildscript {
    ext {
        amgp_version = '2.0'
        jfrog_version = '1.7.3'
        kotlin_version = '1.2.0-rc-84'
        // Release version of the artifact
        release_version = '2.0.0.0'

        dokka_version = '0.9.15'
    }
    repositories {
        maven {
            // URL is defined in gradle.properties 
            url kotlinBintray
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        //Kotlin Dependency
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        //Bintray Dependency
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$jfrog_version"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$amgp_version"

        //Shadow jar Dependency
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'

        //Documentation Dependency
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"

        //node install dependency
        classpath 'com.moowork.gradle:gradle-node-plugin:0.12'
    }
}

plugins {
    // Linting plugin for gradle files
    // Rules are located in gradle/lintRules.gradle
    id 'nebula.lint' version '8.3.1'
}

apply plugin: 'kotlin-platform-common'

allprojects {
    // Apply linting rules to all projects
    apply from: "$project.rootDir/gradle/lintRules.gradle"

    // Manage global repository dependencies
    repositories {
        maven {
            url kotlinBintray
        }
        mavenCentral()
        jcenter()
    }
}

dependencies {
    // Kotlin standard library is needed for the multi-platform files in the 
    // source directory of the project root
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
}
