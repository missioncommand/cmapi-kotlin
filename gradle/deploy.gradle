apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'nebula.maven-base-publish'
apply plugin: 'nebula.maven-nebula-publish'
apply plugin: 'nebula.source-jar'
apply plugin: 'nebula.javadoc-jar'

def artifact = new Properties()
artifact.load(new FileInputStream("$project.rootDir${File.separator}gradle${File.separator}deploy.settings"))

version=release_version
group=artifact.groupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId artifact.groupId
            artifactId artifact.id
            version release_version
            description artifact.description
            name artifact.id // pom.project.name must be same as bintray.pkg.name

            licenses {
                license { // HARDCODED
                    name 'Apache-2.0'
                    distribution 'repo'
                }
            }
            scm {
                connection artifact.gitUrl
                developerConnection artifact.gitUrl
            }
        }
    }
}

allprojects{

    apply plugin: 'idea'
    apply plugin: 'com.jfrog.bintray'

    version = release_version

    bintray {
        /**
         * Developers on this project need their login information
         * setup as follows:
         *
         * Set gradle variables in ~/.gradle/gradle.properties
         *     bintrayUser = john.doe
         *     bintrayApiKey = <located in your Bintray account under edit Profile -> API keys>
         *
         *     * Be sure to set your ~/.gradle/gradle.properties
         *     permissions to 600
         */
        user = bintrayUser
        key = bintrayApiKey
        description = artifact.description
        configurations = ['archives']
        publications = ['nebula']
        pkg {
            userOrg = "missioncommand"
            repo = "maven"
            name = artifact.id
            desc = artifact.description

            licenses = ['Apache-2.0']

            vcsUrl = artifact.gitUrl
            websiteUrl = artifact.siteUrl
            attributes = [:]

            githubReleaseNotesFile = 'README.md'
            githubRepo = 'missioncommand/cmapi-kotlin'

            publish = true
            version {
                name = artifact.version
                vcsTag     = "v${project.version}"
                attributes = [:]
            }
        }
    }
}

publishing.publications {
    nebula(MavenPublication) {
    }
}